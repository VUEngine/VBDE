::ALLOW ACCESS TO VARIABLES SET BY "GET_TIMESTAMP_DIFF" FUNCTION
SETLOCAL ENABLEDELAYEDEXPANSION

::CONVERT ASSETS
IF EXIST %PROJECT_DIR%\assets\images\ (

	::SWITCH TO IMAGES DIRECTORY
	PUSHD %PROJECT_DIR%\assets\images\

	::MAKE _c DIR IF DOES NOT EXISTS
	IF NOT EXIST _c (
		MKDIR _c
	)

	::DELETE ALL C FILES FOR WHICH THE RESPECTIVE IMAGE FILE WAS DELETED
	::AS WELL AS THE INDEX
	FOR /r _c %%F IN (*) DO (
		SET OBSOLETE=1
		FOR /r . %%I IN (*.png *.bmp *.jpg *.jpeg *.gif *.pcx) DO (
			IF "%%~nF" == "%%~nI" (
				SET OBSOLETE=0
			)
		)
		IF !OBSOLETE! == 1 (
			DEL %%F
		)
	)

	::CONVERT ALL IMAGES
	FOR /r . %%F IN (*.png *.bmp *.jpg *.jpeg *.gif *.pcx) DO (

		::GET TIME DIFFERENCE BETWEEN IMAGE AND C FILE
		IF EXIST %PROJECT_DIR%\assets\images\_c\%%~nF.c (
			CALL :GET_TIMESTAMP_DIFF %%F %PROJECT_DIR%\assets\images\_c\%%~nF.c TIMEDIFF
		) ELSE (
			SET TIMEDIFF=1
		)

		::CONVERT IMAGE IF IT HAS CHANGED
		IF !TIMEDIFF! gtr 0 (
			IF EXIST %%~npF.grit (
				::USE FILE SPECIFIC GRIT CONFIG FILE IF IT EXISTS
				%VBDE%\tools\graphics\grit-0.8.6\grit.exe %%F -o _c\%%~nF -ff %%~npF.grit
				ECHO Converted "%%~nxF" using custom config
			) ELSE (
				::OTHERWISE USE DEFAULT GRIT CONFIG FILE
				%VBDE%\tools\graphics\grit-0.8.6\grit.exe %%F -o _c\%%~nF -ff %VBDE%\system\config\images.grit
				ECHO Converted "%%~nxF"
			)
		)
	)
)


::SWITCH BACK TO PROJECT DIRECTORY
PUSHD %PROJECT_DIR%

::DO NOT PROCESS "GET_TIMESTAMP_DIFF" FUNCTION
GOTO :EOF


:GET_TIMESTAMP_DIFF
IF NOT EXIST %~1 GOTO :EOF
IF NOT EXIST %~2 GOTO :EOF
IF "%~3" == "" GOTO :EOF

FOR %%A IN (%~1) DO SET DATE1=%%~tA
FOR %%A IN (%~2) DO SET DATE2=%%~tA

SET /A TIMESTAMP1=%DATE1:~8,1%*60*24*31*12*10+%DATE1:~9,1%*60*24*31*12+%DATE1:~0,1%*60*24*31*10+%DATE1:~1,1%*60*24*31+%DATE1:~3,1%*60*24*10+%DATE1:~4,1%*60*24+%DATE1:~11,1%*60*10+%DATE1:~12,1%*60+%DATE1:~14,1%*10+%DATE1:~15,1%
SET /A TIMESTAMP2=%DATE2:~8,1%*60*24*31*12*10+%DATE2:~9,1%*60*24*31*12+%DATE2:~0,1%*60*24*31*10+%DATE2:~1,1%*60*24*31+%DATE2:~3,1%*60*24*10+%DATE2:~4,1%*60*24+%DATE2:~11,1%*60*10+%DATE2:~12,1%*60+%DATE2:~14,1%*10+%DATE2:~15,1%
IF "%DATE1:~17,1%" == "P" SET /A TIMESTAMP1=%TIMESTAMP1% +12*60
IF "%DATE2:~17,1%" == "P" SET /A TIMESTAMP2=%TIMESTAMP2% +12*60

SET /A %~3=%TIMESTAMP1% - %TIMESTAMP2%

GOTO :EOF
